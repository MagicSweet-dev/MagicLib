import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = 11
targetCompatibility = 11

group = 'com.magicsweet'
version = '1.2.0-beta41'

// todo plugin yml plugin, separate publish script (.gitignore) publish to remote without including plugin.yml file

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    implementation 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    compileOnly 'me.clip:placeholderapi:2.9.2'

    compile 'org.jetbrains:annotations:16.0.2'

    // JitPack
    compile 'fr.mrmicky:FastInv:3.0.3'
    compile 'fr.mrmicky:FastBoard:1.2.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    enabled = false
    dependsOn(tasks.shadowJar)
}

shadowJar {
    dependencies {
        include(dependency('fr.mrmicky:.*:.*'))
    }
    relocate 'fr.mrmicky', 'com.magicsweet.lib.magiclib.shadow.fr.mrmicky'
    archiveClassifier.set('')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    def file = new File('credentials.txt');

    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url = "sftp://msweet.xyz:22/ivy/maven3"
            credentials {
                username file.readLines().get(0)
                password file.readLines().get(1)
            }
        }
    }
}