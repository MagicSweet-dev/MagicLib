import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'maven-publish'
}


sourceCompatibility = 11
targetCompatibility = 11

group = 'com.magicsweet'
version = '1.1.2.1'

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://jitpack.io'
    }

}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    compile 'org.jetbrains:annotations:16.0.2'

    // JitPack
    compile 'fr.mrmicky:FastInv:3.0.2'
    compile 'fr.mrmicky:FastBoard:1.1.0'
}

jar {
    enabled = false
    dependsOn(tasks.shadowJar)
}

shadowJar {
    dependencies {
        include(dependency('fr.mrmicky:.*:.*'))
    }
    relocate 'fr.mrmicky', 'shadow.fr.mrmicky'
    archiveClassifier.set('')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

publishing {
    def file = new File('credentials.txt');

    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            url = "sftp://msweet.xyz:22/ivy/maven3"
            credentials {
                username file.readLines().get(0)
                password file.readLines().get(1)
            }
        }
    }
}