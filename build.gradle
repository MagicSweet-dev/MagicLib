plugins {
    id 'io.freefair.lombok' version '6.2.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
    
    id 'maven-publish'
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.0'
}

apply from: './plugin.yml.gradle'
if (new File('./maven-publish.gradle').exists()) apply from: './maven-publish.gradle'

sourceCompatibility = 11
targetCompatibility = 11

group = 'com.magicsweet'
version = '1.2.0-rc2-beta9'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
        allowInsecureProtocol true
    }
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    implementation 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    implementation 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    
    implementation 'org.jetbrains:annotations:16.0.2'
    
    implementation 'me.clip:placeholderapi:2.9.2'
    
    // JitPack
    implementation 'fr.mrmicky:FastInv:3.0.3'
    implementation 'fr.mrmicky:FastBoard:1.2.0'
}

jar {
    enabled = false
    dependsOn(tasks.shadowJar)
}

shadowJar {
    dependencies {
        include(dependency('fr.mrmicky:.*:.*'))
    }
    relocate 'fr.mrmicky', 'com.magicsweet.lib.magiclib.shadow.fr.mrmicky'
    archiveClassifier.set('')
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}